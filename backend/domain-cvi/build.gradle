bootJar { enabled = false }
jar { enabled = true }

dependencies {
    api(project(":common-cvi"))
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'com.querydsl:querydsl-jpa'
    api 'com.querydsl:querydsl-core'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    implementation 'org.hibernate.validator:hibernate-validator:6.2.0.Final'

    testImplementation(testFixtures(project(":common-cvi")))

    //migration
    implementation 'org.flywaydb:flyway-core:7.11.2'
}

jacocoTestCoverageVerification { // 코드 커버리지 측정항목 시행
    def Qdomains = []

    for (qPattern in "*.QA".."*.QZ") {
        Qdomains.add(qPattern + "*")
    }

    violationRules { // 위반 규칙
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.85
            }

            excludes = [
                    // 커버리지 제외할 클래스
                    '*.ImageType*',
                    '*.JpaConfiguration*',
                    '*.Sort*',
                    '*.Filter*',
                    '*.BooleanToYNConverter*'
            ] + Qdomains
        }
    }
}
